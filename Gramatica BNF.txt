# Gramatica BNF
# Proyecto 2 - Compiladores
# Daniel Zeas Brown

// programa principal
programa ::= declaraciones_globales procedimientos metodo_main

// vars globales
declaraciones_globales ::= declaraciones_globales declaracion_global | ε

declaracion_global ::= WORLD tipo lista_ids ENDL 
    | WORLD tipo lista_ids ASIGNACION expresion ENDL
    | WORLD tipo ID CORCHETE_IZQ INT_LIT CORCHETE_DER CORCHETE_IZQ INT_LIT CORCHETE_DER ENDL
    | WORLD tipo ID CORCHETE_IZQ INT_LIT CORCHETE_DER CORCHETE_IZQ INT_LIT CORCHETE_DER ASIGNACION LLAVE_IZQ inicializacion_arreglo LLAVE_DER ENDL

procedimientos ::= procedimientos procedimiento | ε

procedimiento ::= GIFT tipo ID PAREN_IZQ parametros PAREN_DER bloque

metodo_main ::= GIFT COAL NAVIDAD PAREN_IZQ PAREN_DER bloque

// params
parametros ::= lista_parametros | ε
lista_parametros ::= lista_parametros COMA parametro | parametro
parametro ::= tipo ID

lista_ids ::= lista_ids COMA ID | ID

tipo ::= INT | FLOAT | BOOLEAN | CHAR | STRING

// bloque de codigo
bloque ::= LLAVE_IZQ sentencias LLAVE_DER
sentencias ::= sentencias sentencia | ε

sentencia ::= declaracion_local | asignacion | expresion ENDL 
    | estructura_control | RETURN expresion ENDL | RETURN ENDL 
    | BREAK ENDL | entrada_salida

// declaracion de variables locales  
declaracion_local ::= LOCAL tipo lista_ids ENDL
    | LOCAL tipo lista_ids ASIGNACION expresion ENDL
    | LOCAL tipo ID CORCHETE_IZQ INT_LIT CORCHETE_DER CORCHETE_IZQ INT_LIT CORCHETE_DER ENDL
    | LOCAL tipo ID CORCHETE_IZQ INT_LIT CORCHETE_DER CORCHETE_IZQ INT_LIT CORCHETE_DER ASIGNACION LLAVE_IZQ inicializacion_arreglo LLAVE_DER ENDL

asignacion ::= ID ASIGNACION expresion ENDL
    | ID CORCHETE_IZQ expresion CORCHETE_DER CORCHETE_IZQ expresion CORCHETE_DER ASIGNACION expresion ENDL

inicializacion_arreglo ::= fila_arreglo
    | inicializacion_arreglo COMA fila_arreglo

fila_arreglo ::= LLAVE_IZQ lista_expresiones LLAVE_DER

lista_expresiones ::= lista_expresiones COMA expresion | expresion

// estructuras de control
estructura_control ::= decide_of | loop_structure | for_structure

decide_of ::= DECIDE OF casos END DECIDE ENDL 
    | DECIDE OF casos ELSE FLECHA bloque END DECIDE ENDL

casos ::= casos caso | caso
caso ::= expresion FLECHA bloque

loop_structure ::= LOOP sentencias EXIT WHEN expresion ENDL sentencias END LOOP ENDL

for_structure ::= FOR PAREN_IZQ asignacion_for expresion ENDL expresion PAREN_DER bloque
asignacion_for ::= tipo ID ASIGNACION expresion ENDL | ID ASIGNACION expresion ENDL

// entrada y salida
entrada_salida ::= SHOW PAREN_IZQ argumentos PAREN_DER ENDL 
    | GET PAREN_IZQ ID PAREN_DER ENDL

argumentos ::= lista_argumentos | ε
lista_argumentos ::= lista_argumentos COMA argumento | argumento
argumento ::= expresion | STRING_LIT

// expresiones (con precedencia correcta)
expresion ::= expresion_logica

expresion_logica ::= expresion_logica OR expresion_and | expresion_and
expresion_and ::= expresion_and AND expresion_relacional | expresion_relacional

expresion_relacional ::= expresion_aritmetica op_relacional expresion_aritmetica | expresion_aritmetica
op_relacional ::= MENOR | MENOR_IGUAL | MAYOR | MAYOR_IGUAL | IGUAL_IGUAL | DIFERENTE

expresion_aritmetica ::= expresion_aritmetica MAS termino 
    | expresion_aritmetica MENOS termino 
    | termino

termino ::= termino MULT factor 
    | termino DIV factor 
    | termino DIV_ENTERA factor 
    | termino MODULO factor 
    | factor

factor ::= base POTENCIA factor | base

base ::= INCREMENTO ID 
    | DECREMENTO ID 
    | MENOS base 
    | NOT base 
    | primario

primario ::= INT_LIT 
    | FLOAT_LIT 
    | BOOL_LIT 
    | CHAR_LIT 
    | ID 
    | ID CORCHETE_IZQ expresion CORCHETE_DER CORCHETE_IZQ expresion CORCHETE_DER 
    | llamada_funcion 
    | PAREN_IZQ expresion PAREN_DER

llamada_funcion ::= ID PAREN_IZQ argumentos PAREN_DER